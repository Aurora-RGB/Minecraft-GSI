plugins {
    //kotlin
    id "org.jetbrains.kotlin.jvm" version "2.3.0"
    id "com.crystaelix.loom" version "1.13-SNAPSHOT"
    id "ploceus" version "1.13-SNAPSHOT"
    id "com.crystaelix.blossom" version "1.3.2"
    id 'com.github.johnrengelman.shadow' version '8.1.1'
}

base.archivesName = modId + "-" + mcVersion + "-" + modPlatform

repositories {
    // Add repositories to retrieve artifacts from, here
    mavenCentral()
    flatDir {
        dirs "libs"
    }
    exclusiveContent {
        forRepository {
            maven {
                url = "https://cursemaven.com/"
            }
        }
        filter {
            includeGroup "curse.maven"
        }
    }
}

ploceus {
    disableLibraryUpgrades()
}

sourceSets {
    configureEach {
        // Make Gradle output resources and classes to the same directory
        output.resourcesDir = java.destinationDirectory = layout.buildDirectory.dir("outputs/${name}")
    }
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(8)
        vendor = JvmVendorSpec.AZUL
    }
    withSourcesJar()
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = "UTF-8"
}

loom {
    silentMojangMappingsLicense()
    legacyForge {}

    runs {
        configureEach {
            property "fabric.log.console.level", "debug"
        }
        client {
            programArg "--username=Dev"
            vmArg "-Xmx7G"
        }
        server {
            vmArg "-Xmx3G"
        }
    }
    generatedIntermediateMappings()
}

dependencies {
    implementation project(":aurora-integration")

    // Kotlin runtime required for integration module
    implementation "org.jetbrains.kotlin:kotlin-stdlib:2.3.0"

    // You are shading this into the jar
    implementation "org.jetbrains.kotlinx:kotlinx-serialization-json:1.10.0"

    // Minecraft dependencies, required
    minecraft "com.mojang:minecraft:1.7.10"
    // While it is possible to only use stable-12 mappings here, it is not recommended as most mods depends on Forge default mappings in a development environment
    mappings loom.layered {
        mcp "de.oceanlabs.mcp:mcp_stable:${mcp_version}"
        mcp "net.minecraftforge:forge:${forge_version}:userdev"
    }
    // Apply ploceus patches
    exceptions ploceus.raven(raven_build)
    signatures ploceus.sparrow(sparrow_build)
    nests ploceus.nests(nests_build)
    legacyForge "net.minecraftforge:forge:${forge_version}:universal"
}

def replacements = [
        "0@VERSION@": version
]

blossom {
    replaceToken(replacements)
}

def manifestAttributes = [
        "Specification-Title": modName,
        "Specification-Vendor": modAuthor,
        "Specification-Version": 1,
        "Implementation-Title": modId,
        "Implementation-Version": version,
        "Implementation-Vendor": modAuthor,
]

jar {
    dependsOn processResources
    manifest.attributes(manifestAttributes)
    // Include Kotlin module classes
    from(project(":aurora-integration").sourceSets.main.output)

    // Shade Kotlin runtime + serialization libs
    from {
        configurations.runtimeClasspath.findAll {
            it.name.contains("kotlin-stdlib") ||
                    it.name.contains("kotlinx-serialization")
        }.collect { it.isDirectory() ? it : zipTree(it) }
    }
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}


sourcesJar {
    manifest.attributes(manifestAttributes)
}
